Процедура Загрузить(КолВоЗаписей=0) Экспорт
	
	Если Валюта.Пустая() ИЛИ Период.ДатаНачала = Дата(1,1,1) ИЛИ Период.ДатаОкончания = Дата(1,1,1) Тогда
		Возврат;
	КонецЕсли;
	
	Если Период.ДатаНачала > Период.ДатаОкончания Тогда
		Возврат;
	КонецЕсли;
	
	Прокси = WSСсылки.CBR_DailyInfoWebServ.СоздатьWSПрокси("http://web.cbr.ru/", "DailyInfo", "DailyInfoSoap");		
	ТипWSПараметраС = Прокси.ФабрикаXDTO.Пакеты.Получить("http://web.cbr.ru/").Получить("EnumValutes");	
	WSПараметрС	   = Прокси.ФабрикаXDTO.Создать(ТипWSПараметраС);
	Попытка
		СправочникВалют = Прокси.EnumValutes(WSПараметрС);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нет подключения к сети";
		Сообщение.Сообщить(); 
		Возврат;
	КонецПопытки;
		
	Попытка
		ЭлементыСпр = СправочникВалют.EnumValutesResult.diffgram.ValuteData.EnumValutes;
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось получить справочник валют";
		Сообщение.Сообщить(); 
		Возврат;
	КонецПопытки;
	
	ТзКодовВалют = Новый ТаблицаЗначений;
	ТзКодовВалют.Колонки.Добавить("КодЦБ");
	ТзКодовВалют.Колонки.Добавить("НазваниеВалюты");
	ТзКодовВалют.Колонки.Добавить("НазваниеВалютыАнг");
	ТзКодовВалют.Колонки.Добавить("Кратность");
	ТзКодовВалют.Колонки.Добавить("КодОбщий");
	ТзКодовВалют.Колонки.Добавить("КодОКОФ");
	ТзКодовВалют.Колонки.Добавить("КодБукв");
	
	Для каждого Элемент Из ЭлементыСпр Цикл
		
		Попытка
			СтрКодОКОФ = Элемент.VnumCode; //у гривны и еще кого-то нет кода класифиикатора по которому мы ищем
		Исключение
			Продолжить;
		КонецПопытки;
		
		НовСтр = ТзКодовВалют.Добавить();
		НовСтр.КодЦБ = СокрЛП(Элемент.Vcode);
		НовСтр.НазваниеВалюты = СокрЛП(Элемент.Vname);
		НовСтр.НазваниеВалютыАнг = СокрЛП(Элемент.VEngname);
		НовСтр.Кратность = Число(Элемент.Vnom);
		НовСтр.КодОбщий = СокрЛП(Элемент.VcommonCode);
		НовСтр.КодОКОФ = Формат(Число(СокрЛП(СтрКодОКОФ)), "ЧЦ=3; ЧДЦ=; ЧВН=; ЧГ=0");
		НовСтр.КодБукв = СокрЛП(Элемент.VcharCode);		
	КонецЦикла; 

	Если ТзКодовВалют.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТзКодовВалют.Индексы.Добавить("КодОКОФ");	
	СтрКодовВалют = ТзКодовВалют.Найти(Валюта.Код, "КодОКОФ");
	Если СтрКодовВалют = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КодЦБ = СтрКодовВалют.КодЦБ;
	
	ТипWSПараметра = Прокси.ФабрикаXDTO.Пакеты.Получить("http://web.cbr.ru/").Получить("GetCursDynamic");	
	WSПараметр	   = Прокси.ФабрикаXDTO.Создать(ТипWSПараметра);
	WSПараметр.FromDate	= Период.ДатаНачала;
	WSПараметр.ToDate	= Период.ДатаОкончания;
	WSПараметр.ValutaCode = КодЦБ;
	
	Попытка
		СправочникВалют = Прокси.GetCursDynamic(WSПараметр);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нет подключения к сети";
		Сообщение.Сообщить(); 	
		Возврат;
	КонецПопытки;
	
	
	Попытка
		Элементы = СправочникВалют.GetCursDynamicResult.diffgram.ValuteData.ValuteCursDynamic;	
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Получить данные не удалось";
		Сообщение.Сообщить(); 
		Возврат;
	КонецПопытки;
	
	мКолвоЗаписей = 0;
		
	Для каждого Элемент Из Элементы Цикл
		//Vcode - цб код валюты
		
		Запись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
		Запись.Валюта = Валюта;
		Запись.Период = XMLЗначение(Тип("Дата"), Элемент.CursDate);
		мКурс = XMLЗначение(Тип("Число"),Элемент.Vcurs);
		мКратность = XMLЗначение(Тип("Число"),Элемент.Vnom);
		мКратность = ?(мКратность = 0, 1, мКратность);
		Запись.Курс = мКурс; 
		Запись.Кратность = мКратность;
		Запись.Записать();
		мКолвоЗаписей = мКолвоЗаписей + 1;
			    	
	КонецЦикла; 
	
	КолВоЗаписей = мКолвоЗаписей;
	
КонецПроцедуры

